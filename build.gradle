plugins {
    id 'java'
    id 'com.bmuschko.docker-java-application' version '6.1.2'
}
apply plugin: 'application'
group 'LavaPodler'
version 'LavaPodler'

sourceCompatibility = 1.8
mainClassName = "fr.unix.lavapod.lavapodler.Main"

repositories {
    mavenCentral()                     // main maven repo
    jcenter()                          // JDA and some other stuff
    maven { url "https://jitpack.io" } // build projects directly from github
    maven { url 'https://dl.bintray.com/sedmelluq/com.sedmelluq' }
}

dependencies {
    compile group: 'space.npstr',            name: 'Magma',                          version: '0.12.3'
    compile group: 'com.sedmelluq',          name: 'lavaplayer',                     version: '1.3.32'
    compile group: 'com.sedmelluq',          name: 'lavaplayer-ext-youtube-rotator', version: '0.1.7'
    compile group: 'com.sedmelluq',          name: 'jda-nas',                        version: '1.1.0'
    compile group: 'com.github.shredder121', name: 'jda-async-packetprovider',       version: '1.3.2-MINN'
    compile group: 'io.nats',                name: 'jnats',                          version:'2.6.6'
    compile group: 'org.json',               name: 'json',                           version: '20180813'
    compile group: 'org.slf4j',              name: 'slf4j-simple',                   version: '1.7.30'
}

jar {
    manifest {
        attributes(
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
            'Main-Class'     : mainClassName,
            'Created-By'     : "Matthieu & UniX",
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

docker {
    javaApplication {
        baseImage = 'openjdk:11-jre-slim'
        maintainer = 'Matthieu P "matthieu.p@unix-corp.tech"'
        jvmArgs = ['-Xmx1G', '-Djdk.tls.client.protocols=TLSv1.1,TLSv1.2']
    }
}
dockerCreateDockerfile {
    environmentVariable 'DOCKER', 'true'
}